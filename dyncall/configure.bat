@ECHO OFF

REM Set default values.
SET BUILD_OS=windows
SET BUILD_ARCH=x86
SET BUILD_TOOL=msvc
SET BUILD_ASM=ml
SET INSTALL_DIR=.


REM Scan arguments.
:MAIN_LOOP


REM Help.
IF [%1]==[] (
	GOTO DONE
) ELSE IF [%1]==[/?] (
	ECHO.
	ECHO.dyncall configuration shell-script
	ECHO.Usage:
	ECHO.  configure.bat [ options ]
	ECHO.
	ECHO.Options:
	ECHO.  /?            print this page
	ECHO.
	ECHO.  /prefix PATH  set installation prefix ^(defaults to './install'^)
	ECHO.                ^(only for use with gmake^)
	ECHO.
	ECHO.  /target-x86   build for x86 architecture
	ECHO.  /target-x64   build for x64 architecture
	ECHO.  /target-psp   build for Playstation Portable ^(PSPSDK^)
	ECHO.  /target-nds   build for Nintendo DS ^(devkitPro, ARM mode ^(not THUMB^)^)
	ECHO.
	ECHO.  /tool-gcc     use GNU Compiler Collection
	ECHO.  /tool-msvc    use Microsoft Visual C++ compiler
	ECHO.
	ECHO.  /asm-as       use the GNU Assembler
	ECHO.  /asm-nasm     use NASM Assembler
	ECHO.  /asm-ml       use Microsoft Macro Assembler
	ECHO.
) ELSE IF [%1]==[/prefix] (
	SET INSTALL_DIR=%2
	SHIFT
) ELSE IF [%1]==[/target-x86] (
	SET BUILD_ARCH=x86
) ELSE IF [%1]==[/target-x64] (
	SET BUILD_ARCH=x64
) ELSE IF [%1]==[/target-psp] (
	SET BUILD_ARCH=mips32
	SET BUILD_OS=psp
) ELSE IF [%1]==[/target-nds] (
	SET BUILD_ARCH=arm9e_arm
	SET BUILD_OS=nds
) ELSE IF [%1]==[/tool-gcc] (
	SET BUILD_TOOL=gcc
        SET BUILD_ASM=as
) ELSE IF [%1]==[/tool-msvc] (
	SET BUILD_TOOL=msvc
        SET BUILD_ASM=ml
) ELSE IF [%1]==[/asm-as] (
	SET BUILD_ASM=as
) ELSE IF [%1]==[/asm-nasm] (
	SET BUILD_ASM=nasm
) ELSE IF [%1]==[/asm-ml] (
	SET BUILD_ASM=ml
) ELSE (
	ECHO Unknown parameter '%1'.
	GOTO DONE
)

SHIFT
GOTO MAIN_LOOP
:DONE

ECHO # auto-generated by configure.bat >ConfigVars
ECHO BUILD_OS=%BUILD_OS%#>>ConfigVars
ECHO BUILD_ARCH=%BUILD_ARCH%#>>ConfigVars
ECHO BUILD_TOOL=%BUILD_TOOL%#>>ConfigVars
ECHO BUILD_ASM=%BUILD_ASM%#>>ConfigVars
ECHO INSTALL_DIR=%INSTALL_DIR%#>>ConfigVars



REM We have to transform some pathes for the nds/devkitPro build.
IF [%BUILD_OS%]==[nds] (

REM Check if DEVKITPRO is set.
IF [%DEVKITPRO%]==[] (
	ECHO ERROR: Environment variable DEVKITPRO must be set to absolute devkitPro path.
) ELSE (
	conf-nds.bat
)

)
